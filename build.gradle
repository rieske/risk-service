plugins {
    id "java"
    id "org.springframework.boot" version "2.2.2.RELEASE"
    id "jacoco"
    id "checkstyle"
    id "com.github.kt3k.coveralls" version "2.9.0"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

sourceSets {
    componentTest {
        java.srcDir file('src/component-test/java')
        resources.srcDir file('src/component-test/resources')
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

task componentTest(type: Test)

tasks {
    wrapper {
        gradleVersion = "5.6.4"
    }

    bootJar {
        archiveFileName = "risk-service.jar"
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    checkstyle {
        configFile = new File(rootDir, "checkstyle.xml")
    }

    componentTest {
        dependsOn(test)
        testClassesDirs = sourceSets.componentTest.output.classesDirs
        classpath = sourceSets.componentTest.runtimeClasspath
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = "full"
    }

    check.dependsOn(componentTest)
}

dependencies {
    implementation(platform("org.springframework.boot:spring-boot-dependencies:2.2.3.RELEASE"))

    implementation("org.springframework.boot:spring-boot-starter-web")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("com.jayway.restassured:rest-assured:2.9.0")

    componentTestImplementation(configurations.testImplementation)
}
